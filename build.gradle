import groovy.xml.XmlSlurper

plugins {
	id 'java'
	id 'idea'
	id 'checkstyle'
	id 'com.diffplug.spotless' version '6.21.0'
	id 'java-library'
	id 'maven-publish'
	id 'signing'
	id 'org.jreleaser' version '1.22.0'
	id 'org.jetbrains.changelog' version '2.5.0'
	id 'pl.allegro.tech.build.axion-release' version '1.15.0'
}

scmVersion {
	repository {
		def m2SettingsFile = new File(System.getProperty("user.home"), ".m2/settings.xml")

		def githubServer = null
		if (m2SettingsFile.exists()) {
			def xml = new XmlSlurper().parse(m2SettingsFile)
			githubServer = xml.servers.server.find { it.id.text() == 'github' }
		}

		type = 'git'
		customUsername = findProperty("githubUsername")
				?: System.getenv("GITHUB_USERNAME")
				?: (githubServer?.username?.text() ?: "")
		customPassword = findProperty("githubPassword")
				?: System.getenv("GITHUB_PASSWORD")
				?: (githubServer?.password?.text() ?: "")
	}
	tag {
		prefix = ''
	}
	versionIncrementer 'incrementMinor'
//	versionIncrementer 'incrementPatch'
}

tasks.register('printVersion') {
	doLast {
		println "Current version: ${scmVersion.version}"
	}
}

allprojects {
	group = 'org.daiitech.naftah'
	version = scmVersion.version
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
}

description = "Naftah Programming Language – Write programs in Tunisian/Arabic naturally, like living in Naftah"

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

ext.sharedJvmArgs = [
		'--enable-preview',
		'--add-modules', 'jdk.incubator.vector',

		// Core reflection & language
		'--add-opens', 'java.base/java.lang=ALL-UNNAMED',
		'--add-opens', 'java.base/java.lang.invoke=ALL-UNNAMED',
		'--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED',
		'--add-opens', 'java.base/sun.invoke.util=ALL-UNNAMED',

		// Collections, concurrency, streams, time
		'--add-opens', 'java.base/java.util=ALL-UNNAMED',
		'--add-opens', 'java.base/java.util.concurrent=ALL-UNNAMED',
		'--add-opens', 'java.base/java.util.stream=ALL-UNNAMED',
		'--add-opens', 'java.base/java.time=ALL-UNNAMED',

		// IO & Networking
		'--add-opens', 'java.base/java.io=ALL-UNNAMED',
		'--add-opens', 'java.base/java.net=ALL-UNNAMED',
		'--add-opens', 'java.base/sun.net=ALL-UNNAMED',
		'--add-opens', 'java.base/sun.net.www.protocol.jar=ALL-UNNAMED',
		'--add-opens', 'java.base/sun.net.www.protocol.file=ALL-UNNAMED',

		// IO / Compression
		'--add-opens', 'java.base/java.util.zip=ALL-UNNAMED',
		'--add-opens', 'java.base/java.util.jar=ALL-UNNAMED',

		// Security
		'--add-opens', 'java.base/java.security=ALL-UNNAMED',
		'--add-opens', 'java.base/sun.security.util=ALL-UNNAMED',
		'--add-opens', 'java.base/sun.security.x509=ALL-UNNAMED',

		// Math
		'--add-opens', 'java.base/java.math=ALL-UNNAMED',

		// NIO internals
		'--add-opens', 'java.base/java.nio=ALL-UNNAMED',
		'--add-opens', 'java.base/java.nio.channels=ALL-UNNAMED',
		'--add-opens', 'java.base/java.nio.channels.spi=ALL-UNNAMED',
		'--add-opens', 'java.base/java.nio.charset=ALL-UNNAMED',
		'--add-opens', 'java.base/java.nio.file=ALL-UNNAMED',
		'--add-opens', 'java.base/java.nio.file.spi=ALL-UNNAMED',
		'--add-opens', 'java.base/java.nio.file.attribute=ALL-UNNAMED',
		'--add-opens', 'java.base/sun.nio=ALL-UNNAMED',
		'--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
		'--add-opens', 'java.base/sun.nio.cs=ALL-UNNAMED',
		'--add-opens', 'java.base/sun.nio.fs=ALL-UNNAMED',

		// Reflection utilities
		'--add-opens', 'java.base/sun.reflect.annotation=ALL-UNNAMED',
		'--add-opens', 'java.base/sun.reflect.misc=ALL-UNNAMED',

		// XML / JAXP
		'--add-opens', 'java.xml/com.sun.org.apache.xerces.internal.parsers=ALL-UNNAMED',
		'--add-opens', 'java.xml/com.sun.org.apache.xerces.internal.dom=ALL-UNNAMED',
		'--add-opens', 'java.xml/com.sun.org.apache.xerces.internal.jaxp=ALL-UNNAMED',
		'--add-opens', 'java.xml/com.sun.org.apache.xalan.internal.xsltc.trax=ALL-UNNAMED',

		// Desktop / Swing / AWT
		'--add-opens', 'java.desktop/java.awt=ALL-UNNAMED',
		'--add-opens', 'java.desktop/sun.awt=ALL-UNNAMED',
		'--add-opens', 'java.desktop/javax.swing=ALL-UNNAMED',
		'--add-opens', 'java.desktop/javax.swing.plaf.basic=ALL-UNNAMED',
		'--add-opens', 'java.desktop/sun.java2d=ALL-UNNAMED',

		// Reflection / GC internals
		'--add-opens', 'java.base/java.lang.ref=ALL-UNNAMED',

		// Internal JDK packages
		'--add-opens', 'java.base/jdk.internal.ref=ALL-UNNAMED',
		'--add-opens', 'java.base/jdk.internal.misc=ALL-UNNAMED',
		'--add-opens', 'java.base/jdk.internal.vm=ALL-UNNAMED',
		'--add-opens', 'java.base/jdk.internal.logger=ALL-UNNAMED',
		'--add-opens', 'java.base/jdk.internal.event=ALL-UNNAMED',
		'--add-opens', 'java.base/jdk.internal.loader=ALL-UNNAMED',
		'--add-opens', 'java.base/jdk.internal.module=ALL-UNNAMED'
]

tasks.register('javadocAll', Javadoc) {
	description = 'Generates combined Javadoc for all subprojects (annotations + main)'
	group = 'documentation'

	// Collect all Java sources from subprojects
	source subprojects.collect { it.sourceSets.main.allJava }

	// Combine all compile classpaths
	classpath = files(subprojects.collect { it.sourceSets.main.compileClasspath })

	// Output directory
	destinationDir = layout.buildDirectory.dir("docs/javadoc").get().asFile

	// Reuse same options
	options {
		encoding = 'UTF-8'
		charSet = 'UTF-8'
		memberLevel = JavadocMemberLevel.PRIVATE
		author = true
		version = true
		windowTitle = "${rootProject.name} ${rootProject.version} API"
		docTitle = "${rootProject.name} API Documentation"
		addBooleanOption('Xdoclint:none', true)
	}

	options.addStringOption('--enable-preview')
	options.addStringOption('-add-modules', 'jdk.incubator.vector')

	// Ensure all subprojects are compiled first
	dependsOn subprojects.collect { it.tasks.named('classes') }
}

tasks.register('copyJavadocAll', Copy) {
	description = 'Copies combined Javadoc to docs-site/javadoc'
	group = 'documentation'

	dependsOn tasks.named('javadocAll')

	from tasks.named('javadocAll').get().destinationDir
	into "$rootDir/docs-site/javadoc"
}

changelog {
	version = project.version
	path = 'CHANGELOG.md'
	header = '# Changelog'
	unreleasedTerm = 'Unreleased'
	keepUnreleasedSection = true
	repositoryUrl = 'https://github.com/Chakib-DAII/Naftah'
	versionPrefix = ''
}

tasks.named('jreleaserPrepare') {
	dependsOn tasks.named('patchChangelog')
}

tasks.withType(PublishToMavenRepository).configureEach {
	dependsOn(
			tasks.named('createJReleaserStagingDir'),
			project(":naftah-lang").tasks.named("dist"),
			project(":naftah-lang").tasks.named("distStandalone")
	)
}

jreleaser {
	gitRootSearch = true
	project {
		name = 'Naftah'
		version = rootProject.version
		description = 'Naftah Programming Language'
		longDescription = rootProject.description
		license = 'Apache-2.0'
		inceptionYear = '2026'
		copyright = 'Copyright © ${LocalDate.now().year} The Naftah Project Authors'
		vendor = 'Chakib Daii'
		authors = ['Chakib Daii']

		website = 'https://naftah.daiitech.org'

		links {
			homepage = 'https://naftah.daiitech.org'
			documentation = 'https://naftah.daiitech.org'
			bugTracker = 'https://github.com/Chakib-DAII/Naftah/issues'
			vcsBrowser = 'https://github.com/Chakib-DAII/Naftah'
			contribute = 'https://github.com/Chakib-DAII/Naftah/blob/main/CONTRIBUTING.md'
		}
	}

	environment {
		strict = false
		// Path to your external properties file
		variables = "${System.getProperty('user.home')}/.jreleaser/config.properties"
	}

	release {
		enabled = true
		github {
			enabled = true
			draft = true
			branch = 'main'
			tagName = rootProject.version
			changelog {
				external = "${rootProject.rootDir}/CHANGELOG.md"
				format = 'markdown'
			}
			commitAuthor {
				name = "The Naftah Project Authors"
			}
		}
	}

	signing {
		signing {
			pgp {
				active = 'ALWAYS'
				armored = true
				mode = 'MEMORY'
				secretKey.set(System.getenv("JRELEASER_GPG_SECRET_KEY"))
				publicKey.set(System.getenv("JRELEASER_GPG_PUBLIC_KEY"))
				passphrase.set(System.getenv("JRELEASER_GPG_PASSPHRASE"))
			}
		}
	}

	deploy {
		maven {
			// Maven Central Portal deployment
			mavenCentral {
				sonatype {
					active = 'ALWAYS'
					url = 'https://central.sonatype.com/api/v1/publisher'
					subprojects.forEach { subproject ->
						stagingRepository(subproject.layout.buildDirectory.dir('staging-deploy').get().asFile.path)
					}
				}
			}
			// Nexus2 deployer for snapshots
			nexus2 {
				'snapshot-deploy' {
					active = 'SNAPSHOT'
					snapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
					applyMavenCentralRules = true
					snapshotSupported = true
					closeRepository = true
					releaseRepository = true
					subprojects.forEach { subproject ->
						stagingRepository(subproject.layout.buildDirectory.dir('staging-deploy').get().asFile.path)
					}
				}
			}

			github {
				'github-deploy' {
					active = 'ALWAYS'
					url = 'https://maven.pkg.github.com/Chakib-DAII/Naftah'
					applyMavenCentralRules = false
					snapshotSupported = true
					subprojects.forEach { subproject ->
						stagingRepository(subproject.layout.buildDirectory.dir('staging-deploy').get().asFile.path)
					}
				}
			}
		}
	}
	def version = rootProject.version
	distributions {
		naftahCore {
				distributionType = 'JAVA_BINARY'
				stereotype = 'NONE'

				tags = [
						'cli',
						'naftah',
						'programming',
						'language',
						'Arabic',
						'Tunisian',
				]


				artifact {
					path = project(":naftah-builtin-core")
							.layout.buildDirectory.file(
							"libs/naftah-builtin-core-${version}.jar"
					).get().asFile.absolutePath
				}
		}

		naftahLang {
			distributionType = 'JAVA_BINARY'
			stereotype = 'CLI'

			tags = [
					'cli',
					'naftah',
					'programming',
					'language',
					'Arabic',
					'Tunisian',
					'REPL'
			]

			executable {
				name = 'naftah'
			}

			artifact {
				path = project(":naftah-lang")
						.layout.buildDirectory.file(
						"libs/naftah-lang-${version}.jar"
				).get().asFile.absolutePath
			}
		}

		naftahLangBin {
			distributionType = 'JAVA_BINARY'
			stereotype = 'CLI'

			tags = [
					'cli',
					'naftah',
					'programming',
					'language',
					'Arabic',
					'Tunisian',
					'REPL'
			]

			executable {
				name = 'naftah'
				unixExtension = 'sh'
				windowsExtension = 'cmd'
			}

			artifact {
				path = project(":naftah-lang")
						.layout.buildDirectory.file(
						"distributions/naftah-lang-${version}-bin.zip"
				).get().asFile.absolutePath
			}
		}

		naftahLangStandalone {
			distributionType = 'JAVA_BINARY'
			stereotype = 'CLI'

			tags = [
					'cli',
					'naftah',
					'programming',
					'language',
					'Arabic',
					'Tunisian',
					'REPL'
			]

			executable {
				name = 'naftah'
				unixExtension = 'sh'
				windowsExtension = 'cmd'
			}

			artifact {
				path = project(":naftah-lang")
						.layout.buildDirectory.file(
						"distributions/naftah-lang-${version}-standalone-bin.zip"
				).get().asFile.absolutePath
			}
		}
	}

	checksum {
		enabled = true
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'checkstyle'
	apply plugin: 'com.diffplug.spotless'
	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	apply plugin: 'signing'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}

		withSourcesJar()
		withJavadocJar()
	}

	tasks.withType(JavaCompile).configureEach {
		options.encoding = 'UTF-8'
	}

	tasks.withType(ProcessResources).configureEach {
		filteringCharset = 'UTF-8'
	}

	tasks.withType(JavaExec).configureEach {
		jvmArgs += rootProject.ext.sharedJvmArgs
	}

	tasks.withType(Test).configureEach {
		jvmArgs += rootProject.ext.sharedJvmArgs
		useJUnitPlatform()
	}

	tasks.withType(Javadoc).configureEach {
		source = sourceSets.main.allJava
		classpath = sourceSets.main.compileClasspath
		destinationDir = layout.buildDirectory.file("docs/javadoc").get().asFile
		options {
			encoding = 'UTF-8'
			charSet = 'UTF-8'
			memberLevel = JavadocMemberLevel.PRIVATE
			author = true
			version = true
			// Add a window title for the generated docs
			windowTitle = "${project.name} ${project.version} API"
			// Add a doc title on the main page
			docTitle = "${project.name} API Documentation"
			// Show deprecated members in docs
			addBooleanOption('Xdoclint:none', true)  // disables strict lint checks
		}
	}

	dependencies {
		testImplementation(platform('org.junit:junit-bom:5.7.0'))
		testImplementation('org.junit.jupiter:junit-jupiter')
	}

	checkstyle {
		toolVersion = '10.0'
		configFile = file("../config/checkstyle/checkstyle.xml")
		ignoreFailures = false
		showViolations = true
	}

	spotless {
		java {
			eclipse().configFile('../config/eclipse-formatter.xml')
			removeUnusedImports()
			importOrder('java',
					'javax',
					'org',
					'picocli',
					'com',
					'',
					'static java',
					'static javax',
					'static org',
					'static picocli',
					'static com')
			trimTrailingWhitespace()
			endWithNewline()
			indentWithTabs()

			targetExclude('build/generated-src/antlr/main/**/*.java')
		}
		format 'misc', {
			target '*.gradle', '*.md', '.gitignore'
			trimTrailingWhitespace()
			endWithNewline()
		}
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java

				groupId = project.group
				artifactId = project.name
				version = project.version

				pom {
					name = project.name
					description = "Naftah Programming Language module: ${project.name}"
					url = "https://github.com/Chakib-DAII/Naftah"

					licenses {
						license {
							name = "The Apache License, Version 2.0"
							url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
						}
					}

					developers {
						developer {
							id = "chakib-daii"
							name = "Chakib Daii"
							email = "chakiibdaii@gmail.com"
						}
					}

					scm {
						connection = "scm:git:https://github.com/Chakib-DAII/Naftah.git"
						developerConnection = "scm:git:ssh://git@github.com:Chakib-DAII/Naftah.git"
						url = "https://github.com/Chakib-DAII/Naftah"
					}
				}
			}
		}

		repositories {
			maven {
				name = 'jreleaserStaging'
				url = layout.buildDirectory.dir('staging-deploy').get().asFile.toURI()
			}
		}
	}
}
