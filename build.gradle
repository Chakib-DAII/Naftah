import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
	id 'java'
	id 'antlr'
	id 'idea'
	id 'checkstyle'
	id 'com.diffplug.spotless' version '6.21.0'
}

group = 'org.daiitech'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	antlr "org.antlr:antlr4:4.9.3"
	implementation "org.antlr:antlr4-runtime:4.9.3"
	testImplementation(platform('org.junit:junit-bom:5.7.0'))
	testImplementation('org.junit.jupiter:junit-jupiter')
	checkstyle 'com.puppycrawl.tools:checkstyle:10.0'
	implementation 'info.picocli:picocli:4.7.6'
}

tasks.withType(JavaCompile).configureEach  {
	options.encoding = 'UTF-8'
}

tasks.withType(ProcessResources).configureEach  {
	filteringCharset = 'UTF-8'
}

generateGrammarSource {
	maxHeapSize = "128m"
	arguments += ['-package', 'org.daiitech.naftah.core.parser', '-visitor', '-no-listener']
}

compileJava.dependsOn generateGrammarSource

sourceSets {
	generated {
		java.srcDir 'generated-src/antlr/main/'
	}
}

compileJava.source sourceSets.generated.java, sourceSets.main.java

clean {
	delete "generated-src"
}

idea {
	module {
		sourceDirs += file("generated-src/antlr/main/")
	}
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

checkstyle {
	toolVersion = '10.0'
	configFile = file("config/checkstyle/checkstyle.xml")
	ignoreFailures = false
	showViolations = true
}

spotless {
	java {
		// Using Google Java Format for formatting
		googleJavaFormat('1.17.0')
	}
}

// This task formats the code, but doesn't run on build by default
tasks.register('format') {
	group = 'formatting'
	description = 'Formats all Java source files.'

	dependsOn 'spotlessApply'
}
check.dependsOn checkstyleMain, checkstyleTest

// lint + format in one custom task
tasks.register('lintAndFormat') {
	group = 'verification'
	description = 'Run checkstyle lint and auto-format code.'

	dependsOn checkstyleMain, checkstyleTest, spotlessApply
}

tasks.register('generateReleasePropertiesFile') {
	doLast {
		def outputFile = new File("$buildDir/resources/main/META-INF/naftah-release-info.properties")

		// Create properties file and set properties
		outputFile.parentFile.mkdirs()  // Create the META-INF directory if it doesn't exist

		def buildDate = LocalDateTime.now()

		def date = buildDate.format(DateTimeFormatter.ofPattern('dd-MMM-yyyy'))
		def time = buildDate.format(DateTimeFormatter.ofPattern('hh:mm:ss'))

		outputFile.withWriter('utf-8') {
			it.write("""#
#  Licensed to the Apache Software Foundation (ASF) under one
#  or more contributor license agreements.  See the NOTICE file
#  distributed with this work for additional information
#  regarding copyright ownership.  The ASF licenses this file
#  to you under the Apache License, Version 2.0 (the
#  "License"); you may not use this file except in compliance
#  with the License.  You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
#  Unless required by applicable law or agreed to in writing,
#  software distributed under the License is distributed on an
#  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
#  KIND, either express or implied.  See the License for the
#  specific language governing permissions and limitations
#  under the License.
#

ImplementationVersion=${version}
BundleVersion=${version}
BuildDate=$date
BuildTime=$time
Class-Path= ${configurations.runtimeClasspath
					.files
					.collect { "libs/${it.name}" }
					.join(' ')
			}
""")
		}
	}
}

processResources.dependsOn generateReleasePropertiesFile

jar {
	manifest {
		attributes(
				'Main-Class': 'org.daiitech.naftah.Naftah',
				'Implementation-Title': 'NAFTAH',
				'Implementation-Version': project.version,
				'Implementation-Vendor': 'DaiiTech',
				'Built-By': System.getProperty('user.name'),
				'Built-Date':  LocalDateTime.now().format("yyyy-MM-dd'T'HH:mm"),
				// ðŸ‘‡ Class-Path attribute (relative paths to JARs in /libs/)
				'Class-Path': configurations.runtimeClasspath
						.files
						.collect { "libs/${it.name}" }
						.join(' ')
		)
	}
}

// Task to copy dependencies to /build/libs/libs/
tasks.register('copyDependencies', Copy) {
	from configurations.runtimeClasspath
	into "$buildDir/libs/libs"
}

jar.dependsOn processResources
jar.finalizedBy copyDependencies

tasks.register('fatJar', Jar) {
	archiveBaseName = "${project.name}-fat"
	manifest {
		attributes(
				'Main-Class': 'org.daiitech.naftah.Naftah',
				'Implementation-Title': 'NAFTAH',
				'Implementation-Version': project.version,
				'Implementation-Vendor': 'DaiiTech',
				'Built-By': System.getProperty('user.name'),
				'Built-Date':  LocalDateTime.now().format("yyyy-MM-dd'T'HH:mm")
		)
	}
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
	with jar
}